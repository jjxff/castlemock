buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.3.3.RELEASE")
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.3'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'eclipse'
    apply plugin: "jacoco"
    group = "com.castlemock"
    version = '1.20-SNAPSHOT'

    repositories {
        mavenCentral()
    }

    jacocoTestReport {
        reports {
            xml.enabled true
            html.enabled false
        }
    }

    ext.junitVersion = "4.11"
    ext.log4jVersion = "1.2.17"
    ext.guavaVersion = "17.0"
    ext.mockitoVersion = "1.10.19"
    ext.dozerVersion = "5.4.0"
    ext.springSecurityVersion = "4.0.4.RELEASE"
    ext.springFrameworkVersion = "4.2.5.RELEASE"
    ext.wsdl4jVersion = "1.6.3"
    ext.jacksonVersion = "1.9.13"
    ext.tomcatEmbedVersion = "8.0.28"
    ext.jstlVersion = "1.2"
    ext.servletApiVersion = "3.1.0"
    ext.servletJspVersion = "2.2"
    ext.swaggerVersion = "1.0.25"
    ext.ramlVersion = "1.0.6"
    ext.antlrVersion = "4.7"
    ext.cxfVersion = "3.2.1"
    ext.springfoxVersion = "2.8.0"

	compileJava {
		sourceCompatibility = 1.8
		targetCompatibility = 1.8
	}

	compileTestJava {
		sourceCompatibility = 1.8
		targetCompatibility = 1.8
	}

	dependencies {
        compile(group: 'com.google.guava', name: 'guava', version: guavaVersion)
        compile(group: 'log4j', name: 'log4j', version: log4jVersion)
        compile(group: 'net.sf.dozer', name: 'dozer', 'version': dozerVersion){
            exclude(group:'org.slf4j', module:'slf4j-log4j12')
            exclude(group:'log4j', module:'log4j')
        }
        testCompile(group: 'org.mockito', name: 'mockito-all', version: mockitoVersion)
        testCompile(group: 'junit', name: 'junit', version: junitVersion) {
            exclude(group:'org.hamcrest', module:'hamcrest-core')
        }
	}

    sourceSets {
        main {
            java {
                srcDir 'src/main/java'
            }
            resources {
                srcDir 'src/main/resources'
            }
        }
    }
}

project(':code:core:expression') {
    apply plugin: 'antlr'

    description = "Castle Mock core: Expression"
    group = "com.castlemock.core"

    dependencies {
        antlr group: "org.antlr", name: "antlr4", version: antlrVersion
    }

    generateGrammarSource {
        arguments += [
                "-visitor",
                "-long-messages",
                "-package", "com.castlemock.core.expression"]
    }
}

project(':code:core:basis') {
    description = "Castle Mock core: Basis"
    group = "com.castlemock.core"

    dependencies {
        compile(project(":code:core:expression"))
    }

}

project(':code:core:mock:rest') {
    description = "Castle Mock core: Mock REST"
    group = "com.castlemock.core"
    dependencies {
        compile(project(":code:core:basis"))
    }
}

project(':code:core:mock:soap') {
    description = "Castle Mock core: Mock SOAP"
    group = "com.castlemock.core"
    dependencies {
        compile(project(":code:core:basis"))
    }
}

project(':code:core:mock:graphql') {
    description = "Castle Mock core: Mock GraphQL"
    group = "com.castlemock.core"
    dependencies {
        compile(project(":code:core:basis"))
    }
}

project(':code:web:basis') {
    description = "Castle Mock web: Basis"
    apply plugin: 'spring-boot'
    group = "com.castlemock.web"

    bootRepackage {
        enabled = false
    }

    bootRun {
        enabled = false
    }

    configurations {
        providedRuntime
    }

    dependencies {
        compile(project(":code:core:basis"))
        compile(group: 'org.springframework.boot', name: 'spring-boot-starter-web')
        providedRuntime(group: 'org.springframework.boot', name: 'spring-boot-starter-tomcat')
        compile(group: 'org.springframework', name: 'spring-tx', version: springFrameworkVersion)
        compile(group: 'org.springframework.boot', name: 'spring-boot-starter-security')
        compile(group: 'org.springframework.security', name: 'spring-security-taglibs', version: springSecurityVersion)
        compile(group: 'io.springfox', name: 'springfox-swagger2', version: springfoxVersion)
        compile(group: 'io.springfox', name: 'springfox-swagger-ui', version: springfoxVersion)
        testCompile(group: 'org.springframework.boot', name: 'spring-boot-starter-test')
    }
}

project(':code:web:mock:rest') {
    description = "Castle Mock web: Mock REST"
    apply plugin: 'spring-boot'
    group = "com.castlemock.web"

    bootRepackage {
        enabled = false
    }

    bootRun {
        enabled = false
    }

    dependencies {
        compile(project(":code:web:basis"))
        compile(project(":code:core:mock:rest"))
        compile(group: 'io.swagger', name: 'swagger-parser', version: swaggerVersion)
        compile(group: 'org.raml', name: 'raml-parser-2', version: ramlVersion)
        testCompile(group: 'org.springframework.boot', name: 'spring-boot-starter-test')
    }
}


project(':code:web:mock:graphql') {
    description = "Castle Mock web: Mock GraphQL"
    apply plugin: 'spring-boot'
    group = "com.castlemock.web"

    bootRepackage {
        enabled = false
    }

    bootRun {
        enabled = false
    }

    dependencies {
        compile(project(":code:web:basis"))
        compile(project(":code:core:mock:graphql"))
        compile 'com.graphql-java:graphql-java:7.0'
    }
}

project(':code:web:mock:soap') {
    description = "Castle Mock web: Mock SOAP"
    apply plugin: 'spring-boot'
    group = "com.castlemock.web"

    bootRepackage {
        enabled = false
    }

    bootRun {
        enabled = false
    }

    dependencies {
        compile(group: 'org.apache.cxf', name: 'cxf-rt-frontend-jaxws', version: cxfVersion)
        compile(project(":code:web:basis"))
        compile(project(":code:core:mock:soap"))
        compile(group: 'wsdl4j', name: 'wsdl4j', version: wsdl4jVersion)
        testCompile(group: 'org.springframework.boot', name: 'spring-boot-starter-test')
    }
}

project(':code:web:war') {
    description = "Castle Mock web: WAR"
    apply plugin: 'spring-boot'
    apply plugin: 'war'
    group = "com.castlemock.web"

    bootRepackage {
      mainClass = "com.castlemock.war.config.Application"
    }

    war {
      archiveName = 'castlemock.war'
      baseName = 'castlemock'
      rootSpec.eachFile {
        if (it.name == 'application.properties') {
          ant.replace(file: it.file, token: "\${project.version}", value: version)
        }
      }
      from('../../../') {
          include 'LICENSE-3RD-PARTY'
          include 'LICENSE'
          include 'AUTHORS'
      }
    }

    dependencies {
        compile(project(":code:web:basis"))
        compile(project(":code:web:mock:rest"))
        compile(project(":code:web:mock:soap"))
        compile(project(":code:web:mock:graphql"))

        compile(group: 'org.apache.tomcat.embed', name: 'tomcat-embed-jasper', 'version': tomcatEmbedVersion)
        compile(group: 'javax.servlet', name: 'javax.servlet-api', 'version': servletApiVersion)
        compile(group: 'javax.servlet.jsp', name: 'jsp-api', 'version': servletJspVersion)
        compile(group: 'javax.servlet', name: 'jstl', 'version': jstlVersion)
        compile(group: 'org.codehaus.jackson', name: 'jackson-mapper-asl', 'version': jacksonVersion)
        testCompile(group: 'org.springframework.boot', name: 'spring-boot-starter-test')
    }
}
